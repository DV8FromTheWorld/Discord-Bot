import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

def versionObj = new Version(major: 1, minor: 5, revision: 0)
group = 'net.dv8tion'
archivesBaseName = 'Yui'
mainClassName = 'net.dv8tion.discord.Yui'
version = "${versionObj.toString()}"
compileJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.8
targetCompatibility = 1.8

def filteredSourceDir = file("${buildDir}/filtered")

sourceSets {
    // This source set will contain all sources that we filter
    filtered {
        java {
            srcDirs = [
                    filteredSourceDir,
                    'src/test/java',
            ]
        }
    }
}

// copy the main sources and filter any '@buildVersion@' occurrences.
task processVersion (type: Copy) {
    from sourceSets.main.java
    into filteredSourceDir
    filter(ReplaceTokens, tokens: [
            versionMajor: versionObj.getMajor(),
            versionMinor: versionObj.getMinor(),
            versionRevision: versionObj.getRevision(),
            versionBuild: versionObj.getBuild()
    ])
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version
        attributes 'Main-Class': 'net.dv8tion.discord.Yui'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.filtered.java
}

task instrument(type: JavaExec) {
    main = 'com.ea.async.instrumentation.Main'
    classpath = sourceSets.main.compileClasspath
    args "$buildDir"
}

artifacts {
    archives javadocJar, sourcesJar
}
repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

shadowJar {
    archiveFileName = "Yui-${version}-withDependencies.jar"
}
dependencies {
    implementation 'net.dv8tion:JDA:4.2.0_214'
    implementation 'com.github.devoxin:lavaplayer:1.3.78'
    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'org.xerial:sqlite-jdbc:3.20.0'
    implementation 'org.pircbotx:pircbotx:2.1'
    implementation 'commons-io:commons-io:2.5'
    implementation 'org.json:json:20180813'
    implementation 'com.ea.async:ea-async:1.2.3'
}

class Version {
    int major, minor, revision

    String getMajor() {
        "${major}"
    }

    String getMinor() {
        "${minor}"
    }

    String getRevision() {
        "${revision}"
    }

    String getBuild() {
        System.getenv('BUILD_NUMBER') ? System.getenv('BUILD_NUMBER') : 'DEV'
    }

    String toString() {
        "${getMajor()}.${getMinor()}.${getRevision()}_${getBuild()}"
    }
}

// tell the compileJava task to compile the filtered source
compileJava.source = sourceSets.filtered.java
compileJava.dependsOn processVersion

//Creates the w/ dependencies jar.
assemble.dependsOn shadowJar
